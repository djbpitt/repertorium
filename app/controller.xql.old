xquery version "3.1" encoding "UTF-8";

(: External variables available to the controller: 
$exist:path
    The part of the URL after the part that led to the controller. For example:
    /a/b/c.xq
$exist:resource
    The part of the URL after the last / character, usually pointing to a 
    resource. For example: c.xq
$exist:controller
    The part of the URL leading from the prefix (typically /apps) to the controller 
    script. For example, the name of the root collection of your app, for example:
    /repertorium
$exist:root
    The path to where the app is installed, typically xmldb:exist:///db/apps.
:)
declare variable $exist:path external;
declare variable $exist:resource external;
declare variable $exist:controller external;
declare variable $exist:root external;

(: Other variables :)
declare variable $home-page-url := "index";

(: Function to get the extension of a filename: :)
declare function local:get-extension($filename as xs:string) as xs:string {
    let $name := replace($filename, ".*[/\\]([^/\\]+)$", "$1")
    return
        if (contains($name, "."))
        then
            replace($name, ".*\.([^\.]+)$", "$1")
        else
            ""
};

(: If there is no resource specified, go to the home page.
This is a redirect, forcing the browser to perform a redirect. So this request
will pass through the controller again... :)
if ($exist:resource eq "") then
    <dispatch xmlns="http://exist.sourceforge.net/NS/exist">
        <redirect url="{$home-page-url}"/>
    </dispatch>
    
(: Check if there is no extension. If not, assume it is an XQuery script and forward
to the actual XQuery script by inserting the "modules/" step to the subcollection and
adding the ".xql" extension. Because we use forward here, the browser will not be 
informed of the change and the user will see a URL without the "modules/" step in the
path and without the ".xql" extension. :)
else
    if (local:get-extension($exist:resource) eq "") then
        <dispatch xmlns="http://exist.sourceforge.net/NS/exist">
            <forward url="{concat(
                $exist:controller, 
                substring-before($exist:path, $exist:resource), 
                "modules/", 
                $exist:resource, ".xql"
            )}"/>
        </dispatch>
        (: Anything else, pass through: :)
    else
        <ignore xmlns="http://exist.sourceforge.net/NS/exist">
            <cache-control cache="yes"/>
        </ignore>